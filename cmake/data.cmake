set(_dict_compile_list
  af am an ar as az
  ba be bg bn bpy bs
  ca chr cmn crh cs cv cy
  da de
  el en eo es et eu
  fa fi fr
  ga gd gn grc gu
  hak haw he hi hr ht hu hy
  ia id io is it
  ja jbo
  ka kk kl kn kok ko ku ky
  la lb lfn lt lv
  mi mk ml mn mr ms mto mt my
  nci ne nl nog no
  om or
  pap pa piqd pl pt py
  qdb quc qu qya
  ro ru rup
  sd shn si sjn sk sl smj sq sr sv sw
  ta te ti th tk tn tr tt
  ug uk ur uz
  vi
  yue
)

set(DICT_COMPILE_REGEX "" CACHE STRING "Regex filter for languages to compile (ex: en|es)")
message(STATUS "Compiling dictionaries matching regex: ${DICT_COMPILE_REGEX}")
if(NOT DICT_COMPILE_REGEX STREQUAL "")
  list(FILTER _dict_compile_list INCLUDE REGEX "${DICT_COMPILE_REGEX}")
endif()

set(DATA_DIST_ROOT ${CMAKE_CURRENT_BINARY_DIR})
set(DATA_DIST_DIR ${DATA_DIST_ROOT}/espeak-ng-data)
set(PHONEME_TMP_DIR ${DATA_DIST_ROOT}/phsource)
set(DICT_TMP_DIR ${DATA_DIST_ROOT}/dictsource)

set(DATA_SRC_DIR ${CMAKE_CURRENT_SOURCE_DIR}/espeak-ng-data)
set(PHONEME_SRC_DIR ${CMAKE_CURRENT_SOURCE_DIR}/phsource)
set(DICT_SRC_DIR ${CMAKE_CURRENT_SOURCE_DIR}/dictsource)

file(MAKE_DIRECTORY "${DATA_DIST_DIR}")
file(MAKE_DIRECTORY "${DICT_TMP_DIR}")

set(LANG_COPY_REGEX "" CACHE STRING "Regex filter for language folders/files to copy (ex: gmw/en|roa/es)")
message(STATUS "Copying languages matching regex: ${LANG_COPY_REGEX}")
if(LANG_COPY_REGEX STREQUAL "")
  file(COPY "${DATA_SRC_DIR}/lang" DESTINATION "${DATA_DIST_DIR}")
else()
  file(COPY "${DATA_SRC_DIR}/lang" DESTINATION "${DATA_DIST_DIR}" FILES_MATCHING REGEX "${LANG_COPY_REGEX}")
  file(GLOB_RECURSE _dirs LIST_DIRECTORIES true "${DATA_DIST_DIR}/lang/*")
  list(SORT _dirs ORDER DESCENDING) # Delete subfolder before parent
  foreach(_dir IN LISTS _dirs) # Clear out empty folders
    file(GLOB _content "${_dir}/*")
    if(IS_DIRECTORY "${_dir}" AND NOT _content)
      file(REMOVE_RECURSE "${_dir}")
    endif()
  endforeach()
endif()

# file(COPY "${DATA_SRC_DIR}/voices/!v" DESTINATION "${DATA_DIST_DIR}/voices") # Default voice only, no variants
file(COPY "${PHONEME_SRC_DIR}" DESTINATION "${DATA_DIST_ROOT}")

set(ESPEAK_RUN_ENV ${CMAKE_COMMAND} -E env "ESPEAK_DATA_PATH=${DATA_DIST_ROOT}")
# if building with CMAKE_CROSSCOMPILING use the NativeBuild of espeak-ng
if(NATIVEBUILD)
  set(ESPEAK_RUN_CMD ${ESPEAK_RUN_ENV} $ENV{VALGRIND} "${NATIVEBUILD}")
else()
  set(ESPEAK_RUN_CMD ${ESPEAK_RUN_ENV} $ENV{VALGRIND} "$<TARGET_FILE:espeak-ng-bin>")
endif()

add_custom_command(
  OUTPUT "${DATA_DIST_DIR}/intonations"
  COMMAND ${ESPEAK_RUN_CMD} --compile-intonations
  WORKING_DIRECTORY "${PHONEME_SRC_DIR}"
  COMMENT "Compile intonations"
  DEPENDS
    "$<TARGET_FILE:espeak-ng-bin>"
    "${PHONEME_SRC_DIR}/intonation"
)

set(_phon_deps "")

function(check_phon_deps _file)
  set(_file "${PHONEME_SRC_DIR}/${_file}")
  list(APPEND _phon_deps "${_file}")

  file(STRINGS "${_file}" _phon_incs REGEX "include .+")
  list(TRANSFORM _phon_incs REPLACE "^[ \t]*include[ \t]+" "")
  foreach(_inc ${_phon_incs})
    check_phon_deps(${_inc})
  endforeach(_inc)
  set(_phon_deps ${_phon_deps} PARENT_SCOPE)
endfunction(check_phon_deps)

check_phon_deps("phonemes")

add_custom_command(
  OUTPUT
    "${DATA_DIST_DIR}/phondata"
    "${DATA_DIST_DIR}/phondata-manifest"
    "${DATA_DIST_DIR}/phonindex"
    "${DATA_DIST_DIR}/phontab"
  COMMAND ${ESPEAK_RUN_CMD} --compile-phonemes
  WORKING_DIRECTORY "${PHONEME_SRC_DIR}"
  COMMENT "Compile phonemes"
  DEPENDS
    "${DATA_DIST_DIR}/intonations"
    "$<TARGET_FILE:espeak-ng-bin>"
    ${_phon_deps}
)

list(APPEND _dict_targets)

foreach(_dict_name ${_dict_compile_list})
  set(_dict_target "${DATA_DIST_DIR}/${_dict_name}_dict")
  set(_dict_deps "")
  list(APPEND _dict_targets ${_dict_target})
  list(APPEND _dict_deps
    "${DICT_SRC_DIR}/${_dict_name}_rules"
    "${DICT_SRC_DIR}/${_dict_name}_list"
  )

  if(EXISTS "${DICT_SRC_DIR}/extra/${_dict_name}_listx")
    option(EXTRA_${_dict_name} "Compile extra ${_dict_name} dictionary" ON)
    if(EXTRA_${_dict_name})
      list(APPEND _dict_deps "${DICT_SRC_DIR}/extra/${_dict_name}_listx")
    else()
      file(REMOVE "${DICT_TMP_DIR}/${_dict_name}_listx")
    endif()
  elseif(EXISTS "${DICT_SRC_DIR}/${_dict_name}_listx")
    list(APPEND _dict_deps "${DICT_SRC_DIR}/${_dict_name}_listx")
  endif()
  if(EXISTS "${DICT_SRC_DIR}/${_dict_name}_emoji")
    list(APPEND _dict_deps "${DICT_SRC_DIR}/${_dict_name}_emoji")
  endif()

  add_custom_command(
    OUTPUT "${_dict_target}"
    COMMAND ${CMAKE_COMMAND} -E copy ${_dict_deps} "${DICT_TMP_DIR}/"
    COMMAND ${ESPEAK_RUN_CMD} --compile=${_dict_name}
    WORKING_DIRECTORY "${DICT_TMP_DIR}"
    DEPENDS
      "$<TARGET_FILE:espeak-ng-bin>"
      "${DATA_DIST_DIR}/phondata"
      "${DATA_DIST_DIR}/intonations"
      ${_dict_deps}
  )
endforeach()

add_custom_target(
  data ALL
  DEPENDS
    "${DATA_DIST_DIR}/intonations"
    "${DATA_DIST_DIR}/phondata"
    ${_dict_targets}
)
install(DIRECTORY ${DATA_DIST_DIR} DESTINATION share)
